dnl Process this file with autoconf to produce a configure script for libmaa.
dnl
dnl Created: Sun Nov  6 13:56:03 1994 by faith@cs.unc.edu
dnl Revised: Sat Jan 13 11:45:21 1996 by r.faith@ieee.org
dnl Copyright 1994, 1995, 1996 Rickard E. Faith (faith@cs.unc.edu)
dnl
dnl This program is free software; you can redistribute it and/or modify it
dnl under the terms of the GNU General Public License as published by the
dnl Free Software Foundation; either version 1, or (at your option) any
dnl later version.
dnl
dnl This program is distributed in the hope that it will be useful, but
dnl WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License along
dnl with this program; if not, write to the Free Software Foundation, Inc.,
dnl 675 Mass Ave, Cambridge, MA 02139, USA.
dnl
dnl $Id: configure.in,v 1.2 1996/01/15 03:48:02 faith Exp $
dnl
AC_PREREQ(2.1)
AC_REVISION($Revision: 1.2 $)
AC_INIT(maa.h)
AC_CONFIG_HEADER(config.h)

echo Configuring for libmaa
echo .

AC_CANONICAL_HOST
AC_PROG_CC
AC_ISC_POSIX

REALCC="$CC"
if test "$CC" = gcc; then
    CFLAGS='-g -O -Wall';
else
    CFLAGS='-g';
fi

needs_xmalloc=1

PROF=-p
case "$host" in
dnl did include: -Winline
*linux*)
    EXTRACFLAGS="-pipe -Wwrite-strings -Wpointer-arith -Wcast-align \
        -Wmissing-prototypes -Wshadow -Wnested-externs -Waggregate-return";
    PROF=-pg;;
sparc-sun-sunos*) EXTRACFLAGS="-pipe";;
esac

AC_ARG_WITH(prof,
[  --with-prof             with profiling],
CFLAGS="$CFLAGS $PROF")

AC_ARG_WITH(dmalloc,
[  --with-dmalloc          with Gray Watson's debugging malloc],
needs_dmalloc=1
needs_xmalloc=0)

AC_ARG_WITH(checker,
[  --with-checker          with Checker support (Linux only)],
REALCC=checkergcc
EXTRALDFLAGS="")

AC_ARG_WITH(efence,
[  --with-efence           with Electric Fence (by Bruce Perens)],
DMLIBS="-lefence")

AC_ARG_WITH(insure,
[  --with-insure           with Insure support (from ParaSoft Corp.)],
REALCC=insight
EXTRALDFLAGS="")

AC_ARG_WITH(purify,
[  --with-purify           with Purify support (from Pure, Inc.)],
REALCC="purify gcc"
EXTRALDFLAGS="")

dnl Checks for programs.
echo Checking for programs

AC_PROG_INSTALL
AC_PROG_RANLIB
AC_CHECK_PROGS(PERL,perl4 perl)
AC_CHECK_PROGS(LATEX,latex2e latex)
AC_CHECK_PROGS(FIG2DEV,fig2dev)
AC_CHECK_PROGS(DVIPS,dvips)
AC_CHECK_PROGS(REFBIBTEX,refbibtex)
AC_CHECK_PROGS(BIBTEX,bibtex)

echo .
echo Checking for libraries

if test "$needs_dmalloc" = 1; then
    AC_MSG_CHECKING(if dmalloc installed)
    AC_CHECK_LIB(dmalloc,main,
        DMLIBS="-ldmalloc"
        DMCFLAGS="-DDMALLOC_FUNC_CHECK")
fi

if test "$needs_xmalloc" = 1; then
    LIBOBJS="$LIBOBJS xmalloc.o"
fi

echo .
echo Checking for header files

AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(unistd.h strings.h getopt.h limits.h)
AC_CHECK_HEADERS(sys/resource.h sys/time.h sys/param.h sys/wait.h)

echo .
echo Checking for system constants

AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_C_CONST
AC_C_INLINE
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(void *)
AC_CHECK_SIZEOF(long long int)

echo .
echo Checking for library functions

AC_FUNC_ALLOCA
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday strchr strerror memcpy atexit on_exit sysconf)
AC_CHECK_FUNCS(strdup strtol strtoul)
AC_REPLACE_FUNCS(strdup strtol strtoul)

echo .
echo Making output files

CC="$REALCC"
LDFLAGS="$LDFLAGS $EXTRALDFLAGS"

AC_SUBST(EXTRACFLAGS)
AC_SUBST(DMLIBS)
AC_SUBST(DMCFLAGS)
AC_OUTPUT(Makefile doc/Makefile)

date > stamp-h.in
date > stamp-h
echo .
echo Done
